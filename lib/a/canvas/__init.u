import a.webview
import .htmlevents

html_content = r###"
<html>
    <head>
        <style>
            html, body, canvas {
                margin: 0px;
                padding: 0px;
                width: 100%%;
                height: 100%%;
                overflow: hidden;
            }
        </style>
    </head>
    <body>
        <canvas id="canvas"></canvas>
        <script>%s</script>
    </body>
</html>
"### % [htmlevents.CODE]


def init(
    title = '',
    size = [800, 600],
    delegatecls = nil,
) = {
    delegate = nil
    Canvas(webview.init(
        title = title,
        content = html_content,
        size = size,
        handler = def(arg) {
            nonlocal delegate
            parts = arg.split('/').list()
            switch parts[0] {
                'init' => {
                    delegate = delegatecls()
                }
                'click' => {
                    [btn, x, y] = parts[1:]
                    ordie(delegate.click(int(btn), float(x), float(y)))
                }
                'mousemove' => {
                    [x, y] = parts[1:]
                    ordie(delegate.mousemove(float(x), float(y)))
                }
                'mousedown' => {
                    [btn, x, y] = parts[1:]
                    ordie(delegate.mousedown(int(btn), float(x), float(y)))
                }
                'mouseup' => {
                    [btn, x, y] = parts[1:]
                    ordie(delegate.mouseup(int(btn), float(x), float(y)))
                }
                'keydown' => {
                    [key, mods] = parts[1:]
                    mods = mods.split(',').list()
                    ordie(delegate.keydown(key, mods))
                }
                'keyup' => {
                    [key, mods] = parts[1:]
                    mods = mods.split(',').list()
                    ordie(delegate.keyup(key, mods))
                }
                'keypress' => {
                    [key, mods] = parts[1:]
                    mods = mods.split(',').list()
                    ordie(delegate.keypress(key, mods))
                }
                => {
                    print('Unrecognized cmd %r' % [parts])
                }
            }
        },
    ), def(new_delegate) {
        nonlocal delegate
        delegate = new_delegate
    })
}

class Canvas {
    new(wv, swap_delegate) = new(webview=wv, swap_delegate=swap_delegate)

    def run(self) {
        self.webview.run()
    }

    def exit(self) {
        self.webview.exit()
    }

    def delegate(self, delegatecls) {
        delegate = delegatecls()
        (self.swap_delegate)(delegate)
    }

    # Lots of reference from
    # https://developer.mozilla.org/en-US/docs/Web/API/CanvasRenderingContext2D

    # Drawing rectangles
    def clearRect(self, x, y, width, height) {
        self.webview.eval('ctx.clearRect(%r, %r, %r, %r)' % [x, y, width, height])
    }
    def fillRect(self, x, y, width, height) {
        self.webview.eval('ctx.fillRect(%r, %r, %r, %r)' % [x, y, width, height])
    }
    def strokeRect(self, x, y, width, height) {
        self.webview.eval('ctx.strokeRect(%r, %r, %r, %r)' % [x, y, width, height])
    }

    # Drawing Text
    def fillText(self, text, x, y) {
        self.webview.eval('ctx.fillText(%r, %r, %r)' % [text, x, y])
    }
    def strokeText(self, text, x, y) {
        self.webview.eval('ctx.strokeText(%r, %r, %r)' % [text, x, y])
    }

    def __set_font(self, font) {
        font = str(font)
        self.webview.eval('ctx.font = "%s"' % [font])
    }
    async def __get_font(self) = await self.webview.async_eval('ctx.font')

    def __set_fillStyle(self, fillStyle) {
        fillStyle = str(fillStyle)
        self.webview.eval('ctx.fillStyle = "%s"' % [fillStyle])
    }
    async def __get_fillStyle(self) = await self.webview.async_eval('ctx.fillStyle')

    def __set_strokeStyle(self, strokeStyle) {
        strokeStyle = str(strokeStyle)
        self.webview.eval('ctx.strokeStyle = "%s"' % [strokeStyle])
    }
    async def __get_strokeStyle(self) = await self.webview.async_eval('ctx.strokeStyle')
}

class Delegate {
    def click(self, btn, x, y) = nil
    def mousemove(self, x, y) = nil
    def mousedown(self, btn, x, y) = nil
    def mouseup(self, btn, x, y) = nil
    def keydown(self, key, mods) = nil
    def keyup(self, key, mods) = nil
    def keypress(self, key, mods) = nil
}
