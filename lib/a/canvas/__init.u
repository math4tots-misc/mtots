import a.webview
import a.json
import .htmlevents
from .b2d import Batch2D
from .c2d import Canvas2DMixin
from .img import CanvasImageMixin

html_content = r###"
<html>
    <head>
        <style>
            html, body, canvas {
                margin: 0px;
                padding: 0px;
                width: 100%%;
                height: 100%%;
                overflow: hidden;
            }
        </style>
    </head>
    <body>
        <canvas id="canvas"></canvas>
        <script>%s</script>
        %s
    </body>
</html>
"### % [webview.JS_PRELUDE, htmlevents.CODE]


def init(
    title = '',
    size = [800, 600],
    resizable = false,
    debug = false,
    frameless = false,
    delegatecls = nil,
) = {
    delegate = nil
    Canvas(webview.init(
        title = title,
        content = html_content,
        size = size,
        resizable = resizable,
        debug = debug,
        frameless = frameless,
        handler = def(arg) {
            nonlocal delegate
            parts = arg.split('/').list()
            cmd = parts[0]

            switch cmd {
                'init' => {
                    d = delegatecls()
                    if type(d) is Promise {
                        (async def {
                            nonlocal delegate
                            delegate = d.await
                        })().ordie()
                    } else {
                        delegate = d
                    }
                    return
                }
                'debug' => {
                    print(arg)
                    return
                }
            }

            if delegate is nil {
                # Delegate isn't ready to handle events yet
                return
            }

            switch cmd {
                'tick' => {
                    if type(delegate.tick(float(parts[1]))) is Promise {
                        throw(
                            "Due to time sensitive nature of tick(), " +
                            "the tick() method may not be async"
                        )
                    }
                }
                'click' => {
                    [btn, x, y] = parts[1:]
                    ordie(delegate.click(int(btn), float(x), float(y)))
                }
                'mousemove' => {
                    [x, y] = parts[1:]
                    ordie(delegate.mousemove(float(x), float(y)))
                }
                'mousedown' => {
                    [btn, x, y] = parts[1:]
                    ordie(delegate.mousedown(int(btn), float(x), float(y)))
                }
                'mouseup' => {
                    [btn, x, y] = parts[1:]
                    ordie(delegate.mouseup(int(btn), float(x), float(y)))
                }
                'keydown' => {
                    [key, mods] = parts[1:]
                    mods = mods.split(',').list()
                    ordie(delegate.keydown(key, mods))
                }
                'keyup' => {
                    [key, mods] = parts[1:]
                    mods = mods.split(',').list()
                    ordie(delegate.keyup(key, mods))
                }
                'keypress' => {
                    [key, mods] = parts[1:]
                    mods = mods.split(',').list()
                    ordie(delegate.keypress(key, mods))
                }
                'resize' => {
                    [width, height] = parts[1:]
                    ordie(delegate.resize(width, height))
                }
                => {
                    print('Unrecognized cmd %r' % [parts])
                }
            }
        },
    ), def(new_delegate) {
        nonlocal delegate
        delegate = new_delegate
    })
}

class Canvas(Canvas2DMixin, CanvasImageMixin) {
    # Wraps the HTML5 canvas API, largely following the methods on
    # CanvasRenderingContext2D as documented here:
    # https://developer.mozilla.org/en-US/docs/Web/API/CanvasRenderingContext2D
    #
    # Any method that returns a value is async, as the returned value needs to
    # make a round trip to and from the webview.
    #

    new(wv, swap_delegate) = new(webview=wv, swap_delegate=swap_delegate, ctx=nil)

    async def ctx(self) = {
        if self.ctx == nil {
            self.ctx = self.webview.evalr('ctx').await
        }
        self.ctx
    }

    def run(self) {
        self.webview.run()
    }

    def exit(self) {
        self.webview.exit()
    }

    def delegate(self, delegatecls) {
        delegate = delegatecls()
        (self.swap_delegate)(delegate)
    }

    def timeout(self, nsecs) = self.webview.timeout(nsecs)

    def __set_fullscreen(self, flag) {
        # Ugh, this doesn't seem to work consistently...
        # :/ setting fullscreen twice seems to work better?
        self.webview.set_fullscreen(flag)
        self.eval0('updateCanvasDim()')
        self.webview.set_fullscreen(flag)
        self.eval0('updateCanvasDim()')
    }

    def batch(self) = Batch2D(self)

    def evals(self, js) = self.webview.evals(js)
    def eval0(self, js) = self.webview.eval0(js)
    def evalj(self, js) = self.webview.evalj(js)
    def evalr(self, js) = self.webview.evalr(js)
    def evalx(self, js) = self.webview.evalx(js)
    def evalp(self, js) = self.webview.evalp(js)

    async def __get_width(self) = float(self.evals('canvas.width').await)
    async def __get_height(self) = float(self.evals('canvas.height').await)
}

class Delegate {
    def tick(self, timestamp) = nil
    def resize(self, width, height) = nil
    def click(self, btn, x, y) = nil
    def mousemove(self, x, y) = nil
    def mousedown(self, btn, x, y) = nil
    def mouseup(self, btn, x, y) = nil
    def keydown(self, key, mods) = nil
    def keyup(self, key, mods) = nil
    def keypress(self, key, mods) = nil
}
