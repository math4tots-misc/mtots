import a.webview

html_content = r###"
<html>
    <head>
        <style>
            html, body, canvas {
                margin: 0px;
                padding: 0px;
                width: 100%;
                height: 100%;
                overflow: hidden;
            }
        </style>
    </head>
    <body>
        <canvas id="canvas"></canvas>
        <script>
            // event.repeat is unreliable
            // (in particular, it doesn't work on windows)
            // So we keep track keydown/keyup for each key to indicate
            // whether it's repeating
            const downKeys = {};
            const canvas = document.getElementById('canvas');
            const ctx = canvas.getContext('2d');
            canvas.width = canvas.offsetWidth;
            canvas.height = canvas.offsetHeight;
            function newMouseButtonEventListener(type) {
                return function(event) {
                    external.invoke(
                        type +
                        '/' +
                        event.button +
                        '/' +
                        event.clientX +
                        '/' +
                        event.clientY);
                    event.preventDefault();
                };
            }
            function mkke(name, event) {
                var ret = name + '/' + event.key + '/';
                var mods = [];
                if (event.altKey) {
                    mods.push('alt');
                }
                if (event.ctrlKey) {
                    mods.push('ctrl');
                }
                if (event.metaKey) {
                    mods.push('meta');
                }
                if (downKeys[event.key]) {
                    // event.repeat doesn't work on Windows
                    mods.push('repeat');
                }
                if (event.shiftKey) {
                    mods.push('shift');
                }
                return ret + mods.join(',');
            }
            canvas.addEventListener('click', newMouseButtonEventListener('click'));
            canvas.addEventListener('mousedown', newMouseButtonEventListener('mousedown'));
            canvas.addEventListener('mouseup', newMouseButtonEventListener('mouseup'));
            canvas.addEventListener('mousemove', function(event) {
                external.invoke('mousemove/' + event.clientX + '/' + event.clientY);
            });
            window.addEventListener('keydown', function(event) {
                external.invoke(mkke('keydown', event));
                downKeys[event.key] = true;
            });
            window.addEventListener('keyup', function(event) {
                downKeys[event.key] = false;
                external.invoke(mkke('keyup', event));
            });
            window.addEventListener('keypress', function(event) {
                external.invoke(mkke('keypress', event));
            });
            external.invoke('init')
        </script>
    </body>
</html>
"###


def init(
    title = '',
    size = [800, 600],
    delegatecls = nil,
) = {
    delegate = nil
    Canvas(webview.init(
        title = title,
        content = html_content,
        size = size,
        handler = def(arg) {
            nonlocal delegate
            parts = arg.split('/').list()
            switch parts[0] {
                'init' => {
                    delegate = delegatecls()
                }
                'click' => {
                    [btn, x, y] = parts[1:]
                    ordie(delegate.click(int(btn), float(x), float(y)))
                }
                'mousemove' => {
                    [x, y] = parts[1:]
                    ordie(delegate.mousemove(float(x), float(y)))
                }
                'mousedown' => {
                    [btn, x, y] = parts[1:]
                    ordie(delegate.mousedown(int(btn), float(x), float(y)))
                }
                'mouseup' => {
                    [btn, x, y] = parts[1:]
                    ordie(delegate.mouseup(int(btn), float(x), float(y)))
                }
                'keydown' => {
                    [key, mods] = parts[1:]
                    mods = mods.split(',').list()
                    ordie(delegate.keydown(key, mods))
                }
                'keyup' => {
                    [key, mods] = parts[1:]
                    mods = mods.split(',').list()
                    ordie(delegate.keyup(key, mods))
                }
                'keypress' => {
                    [key, mods] = parts[1:]
                    mods = mods.split(',').list()
                    ordie(delegate.keypress(key, mods))
                }
                => {
                    print('Unrecognized cmd %r' % [parts])
                }
            }
        },
    ), def(new_delegate) {
        nonlocal delegate
        delegate = new_delegate
    })
}

class Canvas {
    new(wv, swap_delegate) = new(webview=wv, swap_delegate=swap_delegate)

    def run(self) {
        self.webview.run()
    }

    def exit(self) {
        self.webview.exit()
    }

    def delegate(self, delegatecls) {
        delegate = delegatecls()
        (self.swap_delegate)(delegate)
    }

    def clearRect(self, x, y, width, height) {
        self.webview.eval('ctx.clearRect(%r, %r, %r, %r)' % [x, y, width, height])
    }

    def fillRect(self, x, y, width, height) {
        self.webview.eval('ctx.fillRect(%r, %r, %r, %r)' % [x, y, width, height])
    }

    def __set_font(self, font) {
        font = str(font)
        self.webview.eval('ctx.font = "%s"' % [font])
    }
    async def __get_font(self) = await self.webview.async_eval('ctx.font')

    def __set_fillStyle(self, fillStyle) {
        fillStyle = str(fillStyle)
        self.webview.eval('ctx.fillStyle = "%s"' % [fillStyle])
    }
    async def __get_fillStyle(self) = await self.webview.async_eval('ctx.fillStyle')

    def __set_strokeStyle(self, strokeStyle) {
        strokeStyle = str(strokeStyle)
        self.webview.eval('ctx.strokeStyle = "%s"' % [strokeStyle])
    }
    async def __get_strokeStyle(self) = await self.webview.async_eval('ctx.strokeStyle')
}

class Delegate {
    def click(self, btn, x, y) = nil
    def mousemove(self, x, y) = nil
    def mousedown(self, btn, x, y) = nil
    def mouseup(self, btn, x, y) = nil
    def keydown(self, key, mods) = nil
    def keyup(self, key, mods) = nil
    def keypress(self, key, mods) = nil
}
