import a.canvas

class Foo {
    new(x, y, dx, dy, w, h) = new(x=x, y=y, dx=dx, dy=dy, w=w, h=h)
    def update(self, dt) = {
        self.x = (self.x + dt * self.dx) % self.w
        self.y = (self.y + dt * self.dy) % self.h
    }
}

last_print = nil

class Delegate(canvas.Delegate) {
    static async def __call() = {
        width = cv.width.await
        height = cv.height.await
        new(
            pos=Foo(
                width / 2,
                height / 2,
                1,
                1,
                width,
                height,
            ),
            last_tick=nil,
            width=width,
            height=height,
            cx=nil,
            cy=nil,
        )
    }
    def tick(self, ts) {
        nonlocal last_print
        # print('tick start')
        if last_print is nil or (ts - last_print) > 1 {
            print('tick %r' % [ts])
            last_print = ts
        }
        if self.last_tick is nil {
            self.last_tick = ts
        }
        dt = ts - self.last_tick
        pos = self.pos

        pos.update(dt)
        # print('pos.x = %r, pos.y = %r' % [pos.x, pos.y])

        (async def {
            ctx = cv.ctx().await
            ctx.fillStyle = '#000000'
            ctx.fillRect(0, 0, self.width, self.height).await
            ctx.fillStyle = '#FFFFFF'
            ctx.fillRect(pos.x - 5, pos.y - 5, 10, 10).await

            if self.cx is not nil {
                ctx.fillRect(self.cx - 10, self.cy - 10, 20, 20)
            }
        })().ordie()

        self.last_tick = ts
    }
    def click(self, btn, x, y) {
        self.cx = x
        self.cy = y
    }
    def keydown(self, key, mods) {
        if key == 'Escape' {
            cv.exit()
        }
    }
}

cv = canvas.init(
    title = 'tick sample',
    delegatecls = Delegate,
)
cv.run()
