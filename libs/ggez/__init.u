import _ggez


class Color {
    [opaque]

    static def __call(r, g, b, a=1.0) = {
        opaque = _ggez::new_color(r, g, b, a)
        __malloc(Color, [opaque])
    }
}

BLACK = Color(0, 0, 0)
WHITE = Color(1, 1, 1)
RED = Color(1, 0, 0)
GREEN = Color(0, 1, 0)
BLUE = Color(0, 0, 1)

class Context {
    [opaque]

    static def __call(opaque) = {
        __malloc(Context, [opaque])
    }

    def size(self) = _ggez::size(self.opaque)
}

class MeshBuilder {
    [opaque]

    static def __call() = {
        __malloc(MeshBuilder, [_ggez::new_mesh_builder()])
    }

    def circle(self, center, radius, color) = {
        _ggez::mesh_builder_circle(self.opaque, center, radius, color.opaque)
        self
    }

    def build(self, ctx) = {
        Drawable(_ggez::mesh_builder_build(self.opaque, ctx.opaque))
    }
}

class Drawable {
    [opaque]

    static def __call(opaque) = __malloc(Drawable, [opaque])

    def draw(self, ctx, dest=nil) = {
        dest = dest or [0, 0]
        _ggez::draw(ctx.opaque, self.opaque, dest)
    }
}

def main(
    name="name",
    author="author",
    init=nil,
    update=nil,
    draw=nil,
    mouse_down=nil,
) = {
    _ggez::start(
        name=name,
        author=author,
        init=if init { def(ctx) = init(Context(ctx)) } else { nil },
        update=if update { def(ctx) = update(Context(ctx)) } else { nil },
        draw=if draw { def(ctx) = draw(Context(ctx)) } else { nil },
        mouse_down=if mouse_down { def(ctx, *args) = mouse_down(Context(ctx), *args) } else { nil },
    )
}

def demo() = {

    width = 0
    height = 0
    x = 0
    y = 0

    main(
        name="demo",
        init=def(ctx) {
            nonlocal x, y, width, height
            [width, height] = ctx.size()
            x = width / 2
            y = height / 2
            print('init, size -> ' + str(ctx.size()))
        },
        draw=def(ctx) {
            MeshBuilder().circle([40, 40], 40, BLUE).build(ctx).draw(ctx)
            MeshBuilder().circle([width - 40, height - 40], 40, RED).build(ctx).draw(ctx)
            (
                MeshBuilder()
                    .circle([width - 40, 40], 40, GREEN)
                    .circle([40, height - 40], 40, WHITE)
                    .build(ctx)
                    .draw(ctx)
            )
            MeshBuilder().circle([0, 0], 40, BLUE).build(ctx).draw(ctx, [x, y])
        },
        mouse_down=def(ctx, button, cx, cy) = {
            nonlocal x, y
            print('button = %s, cx = %s, cy = %s' % [button, cx, cy])
            x = cx
            y = cy
        }
    )
}
