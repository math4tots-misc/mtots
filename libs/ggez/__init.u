import _ggez


class Color {
    [opaque]

    static def __call(r, g, b, a=1.0) = {
        opaque = _ggez::new_color(r, g, b, a)
        __malloc(Color, [opaque])
    }
}

BLACK = Color(0, 0, 0)
WHITE = Color(1, 1, 1)
RED = Color(1, 0, 0)
GREEN = Color(0, 1, 0)
BLUE = Color(0, 0, 1)

class Context {
    [opaque]

    def size(self) = _ggez::ctx_size(self.opaque)

    def fps(self) = _ggez::ctx_fps(self.opaque)

    def quit(self) = _ggez::ctx_quit(self.opaque)
}

trait Drawable {
    def draw(self, ctx, dest=nil) = {
        dest = dest or [0, 0]
        _ggez::ctx_draw(ctx.opaque, self.opaque, dest)
    }
}

class MeshBuilder {
    [opaque]

    static def __call() = {
        __malloc(MeshBuilder, [_ggez::new_mesh_builder()])
    }

    def circle(self, center, radius, color, tolerance=2.0) = {
        _ggez::mesh_builder_circle(self.opaque, center, radius, tolerance, color.opaque)
        self
    }

    def polygon(self, points, color) = {
        """
        NOTE: The points must be given in clockwise order!
        """
        _ggez::mesh_builder_polygon(self.opaque, points, color.opaque)
        self
    }

    def rect(self, upper_left_point, width, height, color) = {
        [x, y] = upper_left_point
        self.polygon(
            [
                upper_left_point,
                [x + width, y],
                [x + width, y + height],
                [x, y + height],
            ],
            color,
        )
    }

    def build(self, ctx) = {
        Mesh(_ggez::mesh_builder_build(self.opaque, ctx.opaque))
    }
}

class Mesh(Drawable) {
    [opaque]
}

class SimpleText(Drawable) {
    [opaque]

    static def __call(text, color=nil, scale=nil) = {
        __malloc(SimpleText, [_ggez::new_text_simple(text, color, scale)])
    }
}

get_all_keycodes = _ggez::get_all_keycodes

_app_info_keys = [
    :name,
    :author,
    :sleep_per_frame,
]

_app_callback_keys = [
    :update,
    :draw,
    :mouse_down,
    :key_down,
    :text_input,
]

class App {
    [_info_table, _callback_table]

    static def __call(name='name', author='author', sleep_per_frame=nil, **kwargs) = {
        info_table = Table(
            name=name,
            author=author,
            sleep_per_frame=sleep_per_frame,
        )
        callback_table = (
            _app_callback_keys
                .map(def(key) = [key, Cell(kwargs.get(key, nil))])
                .to(Table)
        )

        "Ensure there are no extraneous fields"
        assert_eq(callback_table.len(), (kwargs + callback_table).len())

        __malloc(App, [info_table, callback_table])
    }

    def update(self, **kwargs) = {
        """
        Replace all the callbacks for an app.
        Any callback that's not specified will be set to nil.
        """
        for key in _app_callback_keys {
            self._callback_table.get(key).set(kwargs.get(key, nil))
        }
    }

    def main(self) {
        """
        Starts the ggez event loop
        """
        _ggez::start(
            context_class=Context,
            **self._info_table.merge(
                _app_callback_keys.map(def(key) = {
                    [key, self._callback_table.get(key, nil)]
                })
                .to(Table)
            )
        )
    }
}

def demo() = {
    import time

    print(get_all_keycodes())

    width = 0
    height = 0
    x = 0
    y = 0
    last_tick_time = nil
    init = false
    print_key = false

    app = App(
        name="demo",
        update=def(ctx) {
            nonlocal last_tick_time, x, y, width, height, init

            if !init {
                [width, height] = ctx.size()
                x = width / 2
                y = height / 2
                print('init, size -> ' + str(ctx.size()))
                init = true
            }

            now = time::now()
            last_tick_time = if last_tick_time is nil { now } else { last_tick_time }

            if now - last_tick_time > .4 {
                y = min(y + 20, height - 40)
                last_tick_time = now
            }
        },
        draw=def(ctx) {
            MeshBuilder().circle([40, 40], 40, BLUE, 1.0).build(ctx).draw(ctx)
            MeshBuilder().circle([width - 40, height - 40], 40, RED, 1.0).build(ctx).draw(ctx)
            (
                MeshBuilder()
                    .circle([width - 40, 40], 40, GREEN, 1.0)
                    .circle([40, height - 40], 40, WHITE, 1.0)
                    .build(ctx)
                    .draw(ctx)
            )
            MeshBuilder().rect([0, 0], 80, 80, BLUE).build(ctx).draw(ctx, [x - 40, y - 40])

            fps = ctx.fps()
            SimpleText('fps = %r' % [fps]).draw(ctx)

            SimpleText('multiline\n  text').draw(ctx, [0, 40])
        },
        mouse_down=def(ctx, button, cx, cy) = {
            nonlocal x, y
            print('button = %s, cx = %s, cy = %s' % [button, cx, cy])
            if button == :left {
                x = cx
                y = cy
            }
        },
        text_input=def(ctx, c) = {
            nonlocal x, y
            r###" print('text_input c = %r' % [c]) "###
            r###" if c == 'a' {
                x = x - 40
            } elif c == 'd' {
                x = x + 40
            } elif c == 'w' {
                y = y - 40
            } elif c == 's' {
                y = y + 40
            } "###
        },
        key_down=def(ctx, key, mods) = {
            nonlocal x, y, print_key

            if print_key {
                print('key = %r, mods= %s' % [key, mods])
            }

            if key is :Left or key is :A {
                x = x - 40
            } elif key is :Right or key is :D {
                x = x + 40
            } elif key is :Up or key is :W {
                y = y - 40
            } elif key is :Down or key is :S {
                y = y + 40
            } elif key is :P {
                print_key = !print_key
            } elif key is :Escape {
                ctx.quit()
            }
        }
    )

    app.main()
}
